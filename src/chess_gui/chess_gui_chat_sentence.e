indexing
	description:	"A sequence of CHAT_PHRASE's for display in the CHAT_OUTPUT%
			% window."
	copyright: "Copyright (c) 2003 Ken Stauffer"
	license: "Eiffel Forum License v2 (see readme.txt)"
	author: "Ken Stauffer"

class CHESS_GUI_CHAT_SENTENCE
inherit
	LINKED_LIST[ CHESS_GUI_CHAT_PHRASE ]
	export
		{ANY} start, forth, item, count, off
		{NONE} all
	end

creation
	make_system, make_hotbabe

feature -- Initialization
	make_system is
		-- a sentence generated by the system
	do
		is_system := True;
		make;
	end

	make_hotbabe is
		-- a chat sentence generated by hotbabe
	do
		is_hotbabe := True;
		make;
	end

feature -- Status Report
	is_system: BOOLEAN;
	is_hotbabe: BOOLEAN;

feature -- Element Change
	append_normal(str: STRING) is
		-- add a string that will appear in a normal font
	require
		str /= Void;
	local
		phrase: CHESS_GUI_CHAT_PHRASE;
	do
		!! phrase.make_normal(str);
		extend(phrase);
	end

	append_bold(str: STRING) is
		-- add a string that will be displayed in a BOLD format
	require
		str /= Void;
	local
		phrase: CHESS_GUI_CHAT_PHRASE;
	do
		!! phrase.make_bold(str);
		extend(phrase);
	end

	append_link(str: STRING; link_data: CHESS_GUI_CHAT_LINK) is
		-- add a string to the sentence as a "link"
	require
		str /= Void;
		link_data /= Void;
	local
		phrase: CHESS_GUI_CHAT_PHRASE;
	do
		!! phrase.make_link(str, link_data);
		extend(phrase);
	end

invariant
	--
	-- 1 and only 1 flag must be set
	--
	is_system implies not is_hotbabe;
	is_hotbabe implies not is_system;
end
